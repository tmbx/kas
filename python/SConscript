# Hey, Emacs! This is a -*- Python -*- file!
#
# Copyright (C) 2006-2012 Opersys inc.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import os
Import('BUILD_ENV opts_dict')

python_FILES = [
    'kanp.py',
    'kcdpg.py',
    'tcp_client.py',
    'kcgi.py',
    'kfs_lib.py',
    'kcd_lib.py',
    'kcd_client.py',
    'kasmodel.py',
    'kasmodeltool.py',
    'ksamba.py']

python_PROGS = [
    'kasmond.py',
    'kcdhelper.py',
    'kcdshell.py']

installs = []

for pf in python_FILES:
    installs.append(BUILD_ENV.Install(dir = Dir(opts_dict['PYTHONDIR']), 
                                      target = pf))

# Predefine LINESEPARATOR or Textfile will choke.
tf_env = BUILD_ENV.Clone()
tf_env.Append(PYTHONDIR = opts_dict['PYTHONDIR'],
              WWWDIR = opts_dict['WWWDIR'])
tf_env['LINESEPARATOR'] = os.linesep

for pyName in python_PROGS:
    shName = os.path.splitext(os.path.basename(pyName))[0]

    tf_env.Textfile(shName, ["#!/bin/sh",
                             "export PYTHONPATH=$PYTHONDIR",
                             "$PYTHONDIR/" + str(pyName) + ' $@'])
    installs += tf_env.Install(target = opts_dict['PYTHONDIR'], 
                               source = pyName)
    shInst = tf_env.Install(target = opts_dict['BINDIR'], 
                            source = shName)
    tf_env.AddPostAction(shInst, Chmod("$TARGET", 0755))
    installs += shInst

Return('installs')
